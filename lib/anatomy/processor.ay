use(require("atomy"))

require("atomy/codeloader")

pretty = require("pretty")

require("fileutils")

traverse = require("anatomy/stages/traverse")
collect = require("anatomy/stages/collect")
resolve = require("anatomy/stages/resolve")
html = require("anatomy/renderers/html")

def(load(input)):
  mod = Atomy CodeLoader require(input)

  part = mod doc
  traversed = traverse over(part)
  collected = collect over(traversed)
  resolved = resolve over(collected)

  resolved

def(process(input, renderer, output = ".")):
  input = File expand-path(input)
  output = File expand-path(output)

  part = load(input)

  unless(Dir exists?(output)):
    Dir mkdir(output)

  unless(Dir exists?(output + "/public")):
    Dir mkdir(output + "/public")

  FileUtils cp-r(
    File expand-path("../../../public", __FILE__) + "/."
    output + "/public")

  renderer render(part, output)
