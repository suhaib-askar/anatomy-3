use("atomy")

data = require("anatomy/data")

pass((a: Array), part) :=
  pass(x, part) for x in a

pass((c: data ResolveElement), part) :=
  pass(c action[part], part)

pass((e: data Element), part) := do:
  unless(e class == data Element):
    warning(.did-not-traverse(e class))

  e dup tap [x]:
    x content = pass(e content, part)

pass((i: data Itemization), part) :=
  i dup tap [x]:
    x elements =
      [ pass(name, part)
        pass(body, part)
      ] for [name, body] in i elements

pass((p: data Paragraph), part) := do:
  p dup tap [x]:
    x content = pass(p content, part)

pass((c: data ResolveBlock), part) :=
  pass(c action[part], part)

pass((b: data Block), part) := do:
  unless(b class == data Block):
    warning(.did-not-traverse(b class))

  b dup tap [x]:
    x content = pass(b content, part)

pass((s: String), _) := s

pass(nil, _) := nil


over(part) := do:
  part body = pass(part body, part)
  over(sub) for sub in part parts
  part
